; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define AppId "8C19967B-1D27-4E6A-85CD-5059912C2788"
#define AppName "Owlyshield Ransom Community"
#define AppVersion "1.0.0"
#define AppPublisher "SitInCloud"
#define AppURL "https://www.owlyshield.com/"
#define AgentName "Owlyshield Service"
#define FsFilter "OwlyshieldRansomFilter"
#define OutputFilename "owlyshield-ransom-community"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={#AppId}
AppName={#AppName}
AppVersion={#AppVersion}
AppVerName={#AppName} {#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={commonpf64}\{#AppName}
DefaultGroupName={#AppName}
PrivilegesRequired=admin
OutputDir=.
OutputBaseFilename={#OutputFilename}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
ArchitecturesInstallIn64BitMode=x64 arm64 ia64
RestartIfNeededByRun=no

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"; LicenseFile: license_en.txt
Name: "fr"; MessagesFile: "compiler:Languages\French.isl"; LicenseFile: license_fr.txt

[CustomMessages]
en.FullInstall=Full installation
en.CompactInstall=Compact installation
en.CustomInstall=Custom installation
en.Service=Owlyshield service
en.TelemetryHelp=Telemetry and help
en.UserInfo=User Information
en.EnterInfo=Please enter your information.
en.Username=Email address:
en.Company=Company/Organization:
en.Country=Country:
en.Phone=Phone number:
en.InvalidEmail=Invalid email address
en.DownloadSuccessTo=Successfully downloaded file to: %s
en.AbortedByUser=Aborted by user.
fr.FullInstall=Installation complète
fr.CompactInstall=Installation compacte
fr.CustomInstall=Installation personnalisée
fr.Service=Service Owlyshield
fr.TelemetryHelp=Télémétrie et Assistance
fr.UserInfo=Informations sur l'Utilisateur
fr.EnterInfo=Veuillez saisir les informations qui vous concernent.
fr.Username=Adresse email :
fr.Company=Société :
fr.Country=Pays :
fr.Phone=Numéro de téléphone :
fr.InvalidEmail=Email invalide
fr.DownloadSuccessTo=Téléchargement réussi vers : %s
fr.AbortedByUser=Annulé par l'utilisateur

[Types]
Name: "full"; Description: "{cm:FullInstall}"
Name: "compact"; Description: "{cm:CompactInstall}"
Name: "custom"; Description: "{cm:CustomInstall}"; Flags: iscustom

[Components]
Name: "service"; Description: "{cm:Service}"; Types: full custom compact; Flags: fixed
Name: "telemetry"; Description: "{cm:TelemetryHelp}"; Types: full

[Files]
Source: "..\..\owlyshield_minifilter\x64\Debug\{#FsFilter}\*"; DestDir: "{app}\{#FsFilter}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\..\owlyshield_predict\target\release\owlyshield_ransom.exe"; DestDir: "{app}\{#AgentName}"; Flags: ignoreversion
Source: "..\..\owlyshield_predict\moonfire-tflite\lib\tensorflowlite_c.dll"; DestDir: "{app}\{#AgentName}"; Flags: ignoreversion 64bit
Source: "..\..\owlyshield_predict\models\*"; DestDir: "{app}\{#AgentName}\models"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\..\tools\rust_win_toast\target\release\RustWindowsToast.exe"; DestDir: "{app}\utils"; Flags: ignoreversion 64bit
Source: "..\..\owlyshield_minifilter\x64\Debug\{#FsFilter}\{#FsFilter}.sys"; DestDir: "{sys}\drivers"; Flags: ignoreversion 64bit
Source: "logo.ico"; DestDir: "{app}"; Flags: ignoreversion 64bit
Source: "exclusions.txt"; DestDir: "{app}\config"; Flags: ignoreversion 64bit
; This file will be downloaded
Source: "{tmp}\vc_redist.x64.exe"; DestDir: "{app}"; Flags: skipifsourcedoesntexist deleteafterinstall external 

[Dirs]
Name: "{app}\debug";
Name: "{app}\utils";
Name: "{app}\log"; Flags: uninsneveruninstall;
Name: "{app}\config"; Flags: uninsneveruninstall;
Name: "{app}\config\threats"; Flags: uninsneveruninstall;

[Code]
Function GetLanguageKey(Param: string) : string;
Begin
  Case ActiveLanguage Of
    'fr': Result:='fr-FR';
    'en': Result:='en-US';
  End;
End;

function ValidateEmail(strEmail : String) : boolean;
var
    strTemp  : String;
    nSpace   : Integer;
    nAt      : Integer;
    nDot     : Integer;
begin
    strEmail := Trim(strEmail);
    nSpace := Pos(' ', strEmail);
    nAt := Pos('@', strEmail);
    strTemp := Copy(strEmail, nAt + 1, Length(strEmail) - nAt + 1);
    nDot := Pos('.', strTemp) + nAt;
    Result := ((nSpace = 0) and (1 < nAt) and (nAt + 1 < nDot) and (nDot < Length(strEmail)));
end;

var
  UserInfoPage: TInputQueryWizardPage;
  UserInfoPageID: Integer;
  DownloadPage: TDownloadWizardPage;

function ValidateInput(Sender: TWizardPage): Boolean;
begin
  Result := True;

  if not ValidateEmail(UserInfoPage.Values[0]) then
  begin
    MsgBox(ExpandConstant('{cm:InvalidEmail}'), mbError, MB_OK);
    Result := False;
  end;
end;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format(ExpandConstant('{cm:DownloadSuccessTo}'), [FileName]));
  Result := True;
end;

procedure InitializeWizard;
begin
  // User Information page, if telemetry component is selected
  UserInfoPage := CreateInputQueryPage(wpSelectComponents, ExpandConstant('{cm:UserInfo}'), ExpandConstant('{cm:EnterInfo}'), '');

  UserInfoPage.Add(ExpandConstant('{cm:Username}'), False);
  UserInfoPage.Add(ExpandConstant('{cm:Company}'), False);
  UserInfoPage.Add(ExpandConstant('{cm:Country}'), False);
  UserInfoPage.Add(ExpandConstant('{cm:Phone}'), False);

  UserInfoPageID := UserInfoPage.ID;
  UserInfoPage.OnNextButtonClick := @ValidateInput;

  // Download page
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
end;

function ShouldSkipPage(PageID: Integer): Boolean;
begin
  // initialize result to not skip any page (not necessary, but safer)
  Result := False;
  // if the page that is asked to be skipped is your custom page, then...
  if PageID = UserInfoPage.ID then
    { if the component is not selected, skip the page }
    Result := not WizardIsComponentSelected('telemetry');
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = wpReady then begin
    DownloadPage.Clear;

    // Add dependencies here.
    If Not IsMsiProductInstalled('{36F68A90-239C-34DF-B58C-64B30153CE35}', PackVersionComponents(14, 30, 30704, 0)) Then
    Begin
      DownloadPage.Add('https://aka.ms/vs/17/release/vc_redist.x64.exe', 'vc_redist.x64.exe', '');
    End;

    DownloadPage.Show;
    try
      try
        DownloadPage.Download;
        Result := True;
      except
        if DownloadPage.AbortedByUser then
          Log(ExpandConstant('{cm:AbortedByUser'))
        else
          SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end else
    Result := True;
end;

function GetUserName(Param: string): string;
begin
  Result := UserInfoPage.Values[0];
end;

function GetUserCompany(Param: string): string;
begin
  Result := UserInfoPage.Values[1];
end;

function GetUserCountry(Param: string): string;
begin
  Result := UserInfoPage.Values[2];
end;

function GetUserPhone(Param: string): string;
begin
  Result := UserInfoPage.Values[3];
end;

[Icons]
Name: "{group}\{cm:UninstallProgram,{#AppName}}"; Filename: "{uninstallexe}"
Name: "{group}\{#AppName}"; Filename: "{app}\{#AgentName}\owlyshield_ransom.exe"; IconFilename: "{app}\logo.ico"; AppUserModelID: "{#AppId}"; AppUserModelToastActivatorCLSID: "{#AppId}"

[Registry]
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "NUM_VERSION"; ValueData: {#AppVersion}; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "DEBUG_PATH"; ValueData: "{app}\debug"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "UTILS_PATH"; ValueData: "{app}\utils"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "CONFIG_PATH"; ValueData: "{app}\config"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "LOG_PATH"; ValueData: "{app}\log"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "APP_ID"; ValueData: {#AppId}; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "LANGUAGE"; ValueData: {code:GetLanguageKey}; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "KILL_POLICY"; ValueData: "KILL"; Flags: uninsdeletekey
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "TELEMETRY"; ValueData: 0; Flags: uninsdeletekey; Components: not telemetry
Root: HKLM64; Subkey: "Software\Owlyshield"; ValueType: string; ValueName: "TELEMETRY"; ValueData: 1; Flags: uninsdeletekey; Components: telemetry
; Interface SitinCloud - Telemetry & Help
Root: HKLM64; Subkey: "Software\Owlyshield\Telemetry"; ValueType: string; ValueName: "CLIENT_ID"; ValueData: {code:GetUserName}; Flags: uninsdeletekey; Components: telemetry
Root: HKLM64; Subkey: "Software\Owlyshield\Telemetry"; ValueType: string; ValueName: "USER"; ValueData: {code:GetUserName}; Flags: uninsdeletekey; Components: telemetry
Root: HKLM64; Subkey: "Software\Owlyshield\Telemetry"; ValueType: string; ValueName: "COMPANY"; ValueData: {code:GetUserCompany}; Flags: uninsdeletekey; Components: telemetry
Root: HKLM64; Subkey: "Software\Owlyshield\Telemetry"; ValueType: string; ValueName: "COUNTRY"; ValueData: {code:GetUserCountry}; Flags: uninsdeletekey; Components: telemetry
Root: HKLM64; Subkey: "Software\Owlyshield\Telemetry"; ValueType: string; ValueName: "PHONE"; ValueData: {code:GetUserPhone}; Flags: uninsdeletekey; Components: telemetry

[Run]
Filename: "{app}\vc_redist.x64.exe"; Parameters: "/passive /norestart /showrmui /showfinalerror"; Flags: skipifdoesntexist waituntilterminated
Filename: "RUNDLL32.EXE"; Parameters: "SETUPAPI.DLL,InstallHinfSection DefaultInstall 132 {app}\{#FsFilter}\{#FsFilter}.inf"; Flags: runhidden
Filename: "xcopy.exe"; Parameters: """C:\Windows\SysWOW64\drivers\{#FsFilter}.sys"" ""C:\Windows\System32\drivers"" /y"; Flags: runhidden
Filename: "sc.exe"; Parameters: "create ""{#AgentName}"" binPath= ""{app}\{#AgentName}\owlyshield_ransom.exe"""; Flags: runhidden
Filename: "sc.exe"; Parameters: "config ""{#AgentName}"" depend= {#FsFilter}"; Flags: runhidden
Filename: "sc.exe"; Parameters: "config ""{#AgentName}"" start= delayed-auto"; Flags: runhidden
Filename: "sc.exe"; Parameters: "start ""{#FsFilter}"""; Flags: runhidden
Filename: "sc.exe"; Parameters: "start ""{#AgentName}"""; Flags: runhidden
Filename: "sc.exe"; Parameters: "query ""{#AgentName}"""; Flags: runhidden

[UninstallRun]
Filename: "sc.exe"; Parameters: "stop ""{#AgentName}"""
Filename: "sc.exe"; Parameters: "stop ""{#FsFilter}"""
Filename: "sc.exe"; Parameters: "delete ""{#AgentName}"""
Filename: "sc.exe"; Parameters: "delete ""{#FsFilter}"""
Filename: "del.exe"; Parameters: """C:\Windows\System32\drivers\{#FsFilter}.sys"""